import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id "io.freefair.lombok" version "6.5.1"
    id 'org.openjfx.javafxplugin' version '0.0.8'
}
allprojects {
    apply plugin: 'java-library';
    apply plugin: "io.freefair.lombok"

    apply from: new File(rootProject.rootDir, '/scripts/compileSub.gradle')

    group = 'ru.spliterash'
    version = '2.0.9'


    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    repositories {
        mavenCentral()
        mavenLocal()

        maven {
            name = 'codemc'
            url = 'https://repo.codemc.org/repository/maven-public/'
        }
        maven {
            url = 'https://jitpack.io'
        }
        maven {
            url = 'https://repo.codemc.org/repository/maven-public'
        }
        maven {
            name = 'spigotmc-repo'
            url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        }
        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/groups/public/'
        }
        maven {
            url "https://repo.clojars.org/"
        }
    }
    processResources {
        filesMatching("*.yml") {
            filter ReplaceTokens, tokens: [version: version]
        }
    }

    dependencies {
        compileOnly group: 'org.jetbrains', name: 'annotations', version: '19.0.0'
    }
}

dependencies {
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    compileOnly 'org.spigotmc:spigot-api:1.18.2-R0.1-SNAPSHOT'
    //compileOnly 'com.github.koca2000:NoteBlockAPI:2.0-SNAPSHOT'
    compileOnly 'com.xxmicloxx:NoteBlockAPI:2.1-SNAPSHOT'
    compileOnly fileTree("libs")

    api 'com.github.cryptomorin:XSeries:9.1.0'
    api 'io.github.bananapuncher714:nbteditor:7.18.3'
    api 'org.bstats:bstats-bukkit:3.0.0'
}

//noinspection GroovyAssignabilityCheck
compileSub(project)


javafx {
    version = "14"
    modules = ['javafx.fxml', 'javafx.controls']
    configuration = 'compileOnly'
}

jar {
    manifest {
        attributes 'Main-Class': 'ru.spliterash.musicbox.fx.FxStart'
    }
}

def path = "ru.spliterash.musicbox.shadow."
shadowJar {
    relocate "com.cryptomorin.xseries", path + "xseries"
    relocate "org.bstats", path + "bstats"
    relocate "io.github.bananapuncher714.nbteditor", path + "nbteditor"
}

tasks.assemble.dependsOn tasks.shadowJar
